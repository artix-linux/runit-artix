# Maintainer: Muhammad Herdiansyah <herdiansyah@netc.eu>

_url="https://github.com/voidlinux/void-packages/raw/master/srcpkgs"

_sed_args=(-e 's|/var/run|/run|g' -e 's|\(/usr\)\?/sbin|/usr/bin|g')

pkgname=lvm2-runit
pkgver=20180429
pkgrel=1
pkgdesc="runit service scripts for lvm2"
arch=('any')
url="https://github.com/artix-linux"
license=('BSD3')
groups=('runit-system')
depends=('lvm2' 'runit')
provides=('lvm2-runscripts' 'init-lvm2')
conflicts=('init-lvm2')
replaces=('lvm2-runscripts')
source=("lvmetad.run::${_url}/lvm2/files/lvmetad/run"
        "git+https://github.com/artix-linux/runit-artix.git#branch=core-split-patch")
md5sums=('a8189ddaba03f16f971db606f36d7cd6'
         'SKIP')

_inst_sv(){
    for file in run finish check; do
        if test -f "$srcdir/$1.$file"; then
            install -Dm755 "$srcdir/$1.$file" "$pkgdir/etc/runit/sv/$1/$file"
            sed "${_sed_args[@]}" -i "$pkgdir/etc/runit/sv/$1/$file"
        fi
    done
}

_inst_sysinit(){
    cd runit-artix/sysinit-modules
    install -Dm755 $1.sh ${pkgdir}/etc/runit/sysinit-services/$1.sh
}

package() {
    _inst_sv 'lvmetad'

    # do we need this?
    # lvmetad activates vg and sysinit lvm is fallback
    _inst_sysinit '05-lvm'
}
